// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Sys.jack

/**
 * A library that supports various program execution services.
 */
class Sys {

    /** Performs all the initializations required by the OS. */
    function void init() {
		do Memory.init();
		do Math.init();
		do Output.init();
		do Screen.init();
		do Keyboard.init();
		do Main.main();
		do Sys.halt();
		return;
    }

    /** Halts the program execution. */
    function void halt() {
		while (true) {
		}
		return;
    }

    /** Waits approximately duration milliseconds and returns.  */
    function void wait(int duration) {
		var int time, counter;
		let time = 0;
		while (time < duration) {
			let counter = 0;
			while (counter < 150) {		// approximately the amount of counting loops that equals 1 millisecond.
				let counter = counter + 1;
			}
			let time = time + 1;
		}
		return;
    }

    /** Displays the given error code in the form "ERR<errorCode>",
     *  and halts the program's execution. */
    function void error(int errorCode) {
		do Output.printString("ERR");
		do Output.printInt(errorCode);
		do Sys.halt();
		return;
    }
}
